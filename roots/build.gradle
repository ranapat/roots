plugins {
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.jetbrainsKotlinAndroid)
}

group = 'com.github.ranapat'
version = version_name

apply from: "./jacoco.gradle"

android {
    namespace 'org.ranapat.roots'
    compileSdk 34

    defaultConfig {
        minSdk 24

        versionCode version_code
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material

    implementation platform(libs.okhttp.bom)
    implementation libs.okhttp
    implementation libs.okhttp.logging.interceptor

    implementation libs.rx.java
    implementation libs.rx.java.android

    implementation libs.jackson.core
    implementation libs.jackson.annotations
    implementation libs.jackson.databind
    implementation libs.jackson.kotlin

    implementation libs.timber

    testImplementation libs.junit
    testImplementation libs.mockito.core
    testImplementation libs.mockito.kotlin
    testImplementation libs.mockk
    testImplementation libs.okhttp.mock.webserver
    testImplementation libs.json

    compileOnly libs.lombok
    annotationProcessor libs.lombok
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.ranapat'
            artifactId = 'roots'
            version = version_name

            afterEvaluate {
                from components.release
            }
        }
    }
}